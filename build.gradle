// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.0-alpha05'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

ext {
    appVersion = [2, 0, 0]
    appVersionName = appVersion.join('.')
    appVersionCode = appVersion.join().toInteger()
    sampleAppVersion = [0, 0, 1].join().toInteger()

    buildTools = '27.0.1'
    targetSdk = 25
    minSdk = 16

    javaVersion = JavaVersion.VERSION_1_8

    supportLibrary = '25.2.0'
    playServices = '9.8.0'
    butterknife = '8.5.1'
    retrofit = '2.2.0'

    autoValue = '1.4.1'
    autoValueGson = '0.4.6'
    autoValueParcel = '0.2.5'

    libraryAndroidConfig = {
        compileSdkVersion targetSdk
        buildToolsVersion buildTools

        defaultConfig {
            minSdkVersion minSdk
            targetSdkVersion targetSdk
            versionName appVersionName
            versionCode appVersionCode

            javaCompileOptions {
                annotationProcessorOptions {
                    includeCompileClasspath = true
                    arguments = ['autovaluegson.defaultCollectionsToEmpty': 'true']
                }
            }
        }

        compileOptions {
            sourceCompatibility javaVersion
            targetCompatibility javaVersion
        }

        buildTypes {
            release {
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
            }
        }

        lintOptions {
            disable 'InvalidPackage'
        }
    }
}
