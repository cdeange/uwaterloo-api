buildscript {
    repositories {
        maven {
            url 'https://maven.fabric.io/public'
        }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.20.1'
    }
}

repositories {
    maven {
        url 'https://maven.fabric.io/public'
    }
    maven {
        url 'https://jitpack.io'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

Properties properties = new Properties()
File propertiesFile = project.rootProject.file('developer.properties')
if (!propertiesFile.exists()) {
    throw new GradleException('developer.properties file cannot be found in root directory')
}
properties.load(new FileInputStream(propertiesFile))

List<String> manifestProperties = [ 'FABRIC_API_KEY' ]

android {
    compileSdkVersion rootProject.targetSdk
    buildToolsVersion '24.0.2'

    defaultConfig {
        minSdkVersion rootProject.minSdk
        targetSdkVersion rootProject.targetSdk
        versionName rootProject.appVersionName
        versionCode rootProject.appVersionCode

        multiDexEnabled true

        jackOptions {
            enabled true
        }
    }

    buildTypes {
        all {
            properties.entrySet().each {
                buildConfigField 'String', it.key, '"' + it.value + '"'
            }

            manifestPlaceholders = manifestProperties.collectEntries {
                [ (it.toLowerCase()): properties.getProperty(it) ]
            }
        }

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' (debug)'
        }

        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
        }
    }

    compileOptions {
        sourceCompatibility rootProject.javaVersion
        targetCompatibility rootProject.javaVersion
    }

    lintOptions {
        abortOnError false
    }

}

def SUPPORT_VERSION = '24.2.0'
def PLAY_VERSION = '9.4.0'

dependencies {
    provided project(':uwaterlooapi-processor')
    compile project(':uwaterlooapi-annotations')

    compile project(':uwapi-library')

    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:design:' + SUPPORT_VERSION
    compile 'com.android.support:appcompat-v7:' + SUPPORT_VERSION
    compile 'com.android.support:cardview-v7:' + SUPPORT_VERSION
    compile 'com.google.android.gms:play-services-maps:' + PLAY_VERSION
    compile 'com.google.android.gms:play-services-location:' + PLAY_VERSION

    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'net.danlew:android.joda:2.9.3'
    compile 'com.github.tajchert:nammu:1.1.1'
    compile 'uk.co.chrisjenx:calligraphy:2.2.0'

    compile('com.crashlytics.sdk.android:crashlytics:2.5.2@aar') {
        transitive = true;
    }
}
